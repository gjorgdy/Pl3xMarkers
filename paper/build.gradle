plugins {
    id 'java'
    alias(libs.plugins.run.paper)
    alias(libs.plugins.shadow)
}

group = 'nl.gjorgdy'

base {
    archivesName = "${rootProject.name}-${project.name}"
    version = rootProject.version
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    implementation(libs.paper.api)
    implementation(libs.shopkeepers)

    implementation project(':core')
    implementation project(':json')
}

compileJava {
    dependsOn(':core:shadowJar')
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("${libs.versions.minecraft.get()}")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [
        version: rootProject.version,
        minecraft_version: libs.versions.minecraft.get()
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
