plugins {
    id "java"
    id "com.modrinth.minotaur" version "2.+"
}

project.version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

tasks {
    jar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE

        from("LICENSE") {
            rename { "${it}_${project.archivesBaseName}" }
        }

        archiveClassifier = ''

        dependsOn(':core:shadowJar', 'paper:build', 'fabric:remapJar')

        def manifestFiles = subprojects.collect {
            var file = it.layout.buildDirectory.file("libs/${project.name}-${it.name}-${project.version}.jar")
            return zipTree(file) ?: null
        }

        from(manifestFiles) {
            exclude("META-INF/MANIFEST.MF")
        }

        doFirst {
            manifestFiles.forEach {
                it.matching { include("META-INF/MANIFEST.MF") }.files.forEach {
                    manifest.from(it)
                }
            }
        }

        manifest {
            attributes(
                    'Main-Class': 'nl.gjorgdy.pl3xmarkers'
            )
        }
    }
}

static String getModrinthToken() {
    return new File('.env')
            .readLines()
            .findAll { it?.trim() && !it.trim().startsWith('#') }
            .collect { it.split('=') }
            .find { it[0] == 'MODRINTH_TOKEN' }[1]
}

static String readChangelog() {
    return new File('changelog.md')
            .readLines()
            .join('\n')
}

modrinth {
    autoAddDependsOn = false
    token = getModrinthToken()
    projectId = project.mod_id
    versionName = "${archives_base_name} ${version}"
    versionNumber = "${version}"
    changelog = readChangelog()
    versionType = project.mod_release_type
    uploadFile = jar
    gameVersions = [project.minecraft_version]
    loaders = ["fabric", "quilt", "bukkit", "paper", "purpur", "folia", "spigot"]
    dependencies {
        required.project "fabric-api"
        required.version "pl3xmap", project.pl3xmap_version
    }
}
