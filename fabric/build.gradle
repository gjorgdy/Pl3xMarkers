plugins {
    alias(libs.plugins.fabric.loom)
}

project.version = "${project.mod_version}+${libs.versions.minecraft.get()}"
group = project.maven_group

base {
    archivesName = "${rootProject.name}-${project.name}"
}

repositories {
    mavenCentral()
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
    maven {url = "https://api.modrinth.com/maven"}
    maven {url = "https://maven.nova-committee.cn/releases/"}
    maven {url = "https://maven.nova-committee.cn/snapshots/"}
    mavenLocal()
}

dependencies {
    minecraft libs.minecraft
    mappings "${libs.yarn.mappings.get()}:v2"

    modImplementation libs.fabric.loader
    modImplementation libs.fabric.api

    modCompileOnly libs.pl3xmap

    modCompileOnly libs.openPartiesAndClaims

    implementation project(':core')
    implementation project(':json')
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", libs.versions.minecraft.get()
    inputs.property "loader_version", libs.versions.fabricLoader.get()

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": libs.versions.minecraft.get(),
                "loader_version": libs.versions.fabricLoader.get()
    }
}

compileJava {
    dependsOn(':core:shadowJar')
}

remapSourcesJar {
    dependsOn(':core:shadowJar')
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

remapJar {
    archiveClassifier.set('')
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}
